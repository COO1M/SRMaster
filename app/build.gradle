plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace "com.master.sr"
    compileSdk 34
    ndkVersion '26.1.10909125'

    defaultConfig {
        applicationId "com.master.sr"
        minSdk 28
        targetSdk 34
        versionCode 12
        versionName "1.2"

        ndk { abiFilters "arm64-v8a" }
        resourceConfigurations["zh"]

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables { useSupportLibrary true }
    }
    signingConfigs {
        debug {
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.5.8"
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
        jniLibs {
            useLegacyPackaging true
        }
    }
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
}

dependencies {
    //AndroidX
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.activity:activity-compose:1.8.2"
    implementation "androidx.navigation:navigation-compose:2.7.6"
    implementation "androidx.core:core-splashscreen:1.0.1"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.1.0-alpha13"
    implementation "androidx.startup:startup-runtime:1.2.0-alpha02"
    //Compose
    def compose_version = "1.6.0"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    //Lifecycle
    def lifecycle_version = "2.7.0"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    //Pytorch
    def torch_version = "2.1.0"
    implementation "org.pytorch:pytorch_android_lite:$torch_version"
    implementation "org.pytorch:pytorch_android_torchvision_lite:$torch_version"
    //Onnx
    implementation "com.microsoft.onnxruntime:onnxruntime-android:1.16.3"
    implementation "com.microsoft.onnxruntime:onnxruntime-extensions-android:0.9.0"
    //XXPermission
    implementation "com.github.getActivity:XXPermissions:18.6"
    //Coil
    implementation "io.coil-kt:coil-compose:2.5.0"
    //MMKV
    implementation "com.tencent:mmkv:1.3.3"
    //UM
    implementation "com.umeng.umsdk:common:9.6.7"
    implementation "com.umeng.umsdk:asms:1.8.2"
}
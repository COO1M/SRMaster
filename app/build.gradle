plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace "com.master.sr"
    compileSdk 33
    buildToolsVersion "33.0.2"
    ndkVersion "25.2.9519653"

    defaultConfig {
        applicationId "com.master.sr"
        minSdk 28
        targetSdk 33
        versionCode 11
        versionName "1.1"

        ndk { abiFilters "arm64-v8a" }
        resourceConfigurations["zh"]

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables { useSupportLibrary true }
    }
    signingConfigs {
        debug {
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.4.7"
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
        jniLibs {
            useLegacyPackaging true
        }
    }
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
    allprojects {
        configurations.all {
            resolutionStrategy {
                force "androidx.collection:collection:1.2.0"
            }
        }
    }
}

dependencies {
    //AndroidX
    implementation "androidx.core:core-ktx:1.11.0-beta02"
    implementation "androidx.activity:activity-compose:1.7.2"
    implementation "androidx.navigation:navigation-compose:2.6.0"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.1.0-alpha10"
    implementation "androidx.startup:startup-runtime:1.2.0-alpha02"
    implementation "androidx.core:core-splashscreen:1.0.1"
    //Compose
    def compose_version = "1.5.0-beta02"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    //Lifecycle
    def lifecycle_version = "2.6.1"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    //Pytorch
    def torch_version = "1.13.1"
    implementation "org.pytorch:pytorch_android_lite:$torch_version"
    implementation "org.pytorch:pytorch_android_torchvision_lite:$torch_version"
    //Onnx
    implementation "com.microsoft.onnxruntime:onnxruntime-android:1.15.0"
    implementation "com.microsoft.onnxruntime:onnxruntime-extensions-android:0.8.0"
    //XXPermission
    implementation "com.github.getActivity:XXPermissions:18.2"
    //Coil
    implementation "io.coil-kt:coil-compose:2.4.0"
    //MMKV
    implementation "com.tencent:mmkv:1.2.16"
    //UM
    implementation "com.umeng.umsdk:common:9.6.1"
    implementation "com.umeng.umsdk:asms:1.8.0"
}